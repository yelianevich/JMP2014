apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: "jacoco"

checkstyle {
	sourceSets = [ project.sourceSets.main, project.sourceSets.test ]
}

tasks.withType(FindBugs) {
	reports {
		xml.enabled = false
		html.enabled = true
	}
}

jacocoTestReport {
	reports {
		xml.enabled = false
		csv.enabled = false
		html.enabled = true
	}
}

jar.manifest.attributes 'Implementation-Title': 'jms'

dependencies {
	compile("com.rabbitmq:amqp-client:$rabbitMqVersion")
}

task workerJar(type: Jar) {
	description = 'Assemble worker runnable jar'
	baseName = 'jms-worker'
	manifest.attributes 'Main-Class': 'com.epam.rabbitmq.workqueue.Worker'
	exclude 'com/epam/rabbitmq/helloworld/**'
	from {
		configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
	} with jar
}

task producerJar(type: Jar) {
	description = 'Assemble producer runnable jar'
	baseName = 'jms-producer'
	manifest.attributes 'Main-Class': 'com.epam.rabbitmq.workqueue.NewTask'
	exclude 'com/epam/rabbitmq/helloworld/**'
	from {
		configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
	} with jar
}

jar.dependsOn(producerJar, workerJar)

