apply plugin: 'sonar-runner'

sonarRunner {
	sonarProperties {
		property 'sonar.projectName', 'jmp'
		property 'sonar.projectDescription', 'Java mentoring program training tasks'
		property "sonar.java.coveragePlugin", "jacoco"
	}
}

allprojects {
	defaultTasks 'build'
	apply plugin: 'eclipse'

	group = "com.epam.jmp"
	version = '0.1-SNAPSHOT'

	repositories {
		jcenter()
	}
}

subprojects {
	apply plugin: 'java'
	apply from: "$rootDir/gradle/checkstyle.gradle"
	apply from: "$rootDir/gradle/findbugs.gradle"
	apply from: "$rootDir/gradle/jacoco.gradle"

	ext {
		bootVersion = '1.2.0.RELEASE'
		hsqldbVersion = '2.3.2'
		springVersion = '4.2.4.RELEASE'
		guavaVersion = '18.0'
		hamcrestVersion = '1.3'
		junitVersion = '4.12'
		mockitoVersion = '1.10.8'
		dbunitVersion = '2.5.0'
		log4jVersion = '2.1'
		rabbitMqVersion = '3.4.3'
		jacksonVersion = '2.4.0'
		jbehaveVersion = '3.9.5'
		slf4jVersion = '1.7.9'
		slf4jLog4j12Version = '1.7.10'
		commonsLang3Version = '3.4'
	}

	compileJava {
		sourceCompatibility = 1.8
		targetCompatibility = 1.8
	}

	compileTestJava {
		sourceCompatibility = 1.8
		targetCompatibility = 1.8
	}

	dependencies {
		compile "org.apache.logging.log4j:log4j-core:$log4jVersion"
		testCompile("junit:junit:$junitVersion") {
			exclude group: 'org.hamcrest', module: 'hamcrest-core'
		}
		testCompile("org.mockito:mockito-core:$mockitoVersion") {
			exclude group: 'org.hamcrest', module: 'hamcrest-core'
		}
		testCompile("org.hamcrest:hamcrest-all:$hamcrestVersion")
	}

	jar {
		manifest {
			attributes provider: 'gradle', 'Implementation-Version': version
		}
	}

	test {
		exclude '**/*Suite.class', '**/*Tests.class'

		//forkEvery = 5
		//maxParallelForks = Runtime.runtime.availableProcessors() / 2

		testLogging {
			showStackTraces = true
			exceptionFormat 'full'
			events 'skipped', 'failed'
		}
	}
	test.addTestListener(new com.epam.jmp.NotificationTestListener(project))

	sonarRunner {
		sonarProperties {
			property 'sonar.sourceEncoding', 'UTF-8'
		}
	}
}

task wrapper(type: Wrapper) {
	description = 'Generate generic scripts to run on *nix and Windows'
	gradleVersion = '2.10'
}